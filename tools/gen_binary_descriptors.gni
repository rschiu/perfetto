# Copyright (C) 2018 The Android Open Source Project
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#      http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("../gn/perfetto.gni")

template("gen_binary_descriptors") {
  assert(defined(invoker.source))

  config_name = "${target_name}_config"
  action_name = "${target_name}"

  protoc_label = "//buildtools:protoc($host_toolchain)"
  protoc_path = get_label_info(protoc_label, "root_out_dir") + "/protoc"

  config(config_name) {
    include_dirs = [ "${root_gen_dir}/${perfetto_root_path}" ]
  }

  action(action_name) {
    script = "//tools/gen_binary_descriptors"

    source_name = get_path_info(invoker.source, "name")
    generated_header = "${target_gen_dir}/${source_name}.descriptor.h"

    inputs = [
      protoc_path,
    ]
    args = [
      "--protoc",
      rebase_path(protoc_path, root_build_dir),
      "--proto-path",
      rebase_path("$perfetto_root_path/protos", root_build_dir),
      rebase_path(invoker.source, root_build_dir),
      rebase_path(generated_header, root_build_dir),
    ]
    outputs = [
      generated_header,
    ]
    deps = [
      protoc_label,
    ]
    public_configs = [ ":$config_name" ]
  }
}
